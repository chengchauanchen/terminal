apply plugin: 'com.android.application'
Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    flavorDimensions "default"
    defaultConfig {
        applicationId "cn.vsx.vc"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 7
        versionName "1.0.6"

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
        ndk {
            abiFilters "armeabi", "armeabi-v7a"
        }

    }

    signingConfigs {
        release {
            storeFile file(props['release.keystore'])
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
            storePassword = props['storePassword']
        }
        debug {
            storeFile file(props['release.keystore'])
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
            storePassword = props['storePassword']
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true//去掉不用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false//去掉不用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        common {}
        policeStore {}
        policeStoreOut {}
        xiangYang {}
        xiangYangPoliceStore {}
        teJing {}
        chuTianYun {}
        jiaXing {}
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [apk_type: name]
    }

    //版本比较多时，自定义导出的APK名称，不同的渠道编出的APK的文件名应该是不一样的
    //Android插件3.0迁移指南所示：
    //用all()而不是each()
    //如果您只更改文件名（这是您的情况），请使用outputFileName而不是output.outputFile
    applicationVariants.all { variant ->
        variant.outputs.all() { output ->
            def flavor = variant.productFlavors[0]
            def fileName = "hdmi_${variant.flavorName}_${signTime()}.${variant.versionName}_${variant.versionCode}.apk"
            /*//默认打包方式，打包到app的子目录下
            outputFileName = fileName*/
            //修改打包路径，工程apkpackage文件夹下
            //也可以在打包时手动选择对应目录
            def des = output.packageApplication.outputDirectory.toPath().relativize(rootDir.toPath()).toFile()
            outputFileName = new File("$des/apkpackage", fileName)
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/plexus/components.xml'
        exclude 'licenses/javolution.license.TXT'
        exclude 'licenses/thoughtworks.TXT'
        exclude 'org/apache/maven/project/pom-4.0.0.xml'
        exclude 'licenses/extreme.indiana.edu.license.TXT'
        exclude 'org/cyberneko/html/res/HTMLlat1.properties'
        exclude 'org/cyberneko/html/res/ErrorMessages_ja.properties'
        exclude 'org/cyberneko/html/res/ErrorMessages_ja.txt'
        exclude 'org/cyberneko/html/res/ErrorMessages.properties'
        exclude 'org/codehaus/plexus/plexus-bootstrap.xml'
        exclude 'org/cyberneko/html/res/HTMLspecial.properties'
        exclude 'org/cyberneko/html/res/XMLbuiltin.properties'
        exclude 'org/cyberneko/html/res/HTMLsymbol.properties'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        ignoreWarnings true
        checkReleaseBuilds false
    }
}

def signTime() {
    return new Date().format("MM-dd")
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:multidex:1.0.3'
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:design:$support_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':SpecificSDK4GPTT')
    implementation 'net.bozho.easycamera:easycamera:0.0.1:aar@aar'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.github.liuguangqiang.swipeback:library:1.0.2@aar'
    implementation files('libs/poi-3.9-20121203.jar')
    implementation files('libs/butterknife-7.0.1.jar')
    implementation files('libs/poi-scratchpad-3.9-20121203.jar')
    implementation(name: 'libuvccamera-release', ext: 'aar') {
        exclude module: 'support-v4'
        exclude module: 'appcompat-v7'
    }

}
