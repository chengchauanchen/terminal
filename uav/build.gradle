//打无人机包用此代码
apply plugin: 'com.android.application'
//apply plugin: 'com.antfortune.freeline'
Properties props = new Properties()
props.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdkVersion 26
    buildToolsVersion '28.0.3'
    flavorDimensions "default"
    defaultConfig {
        applicationId "cn.vsx.vc"
        minSdkVersion 19
        targetSdkVersion 26


        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }
        ndk {
            abiFilters "armeabi", "armeabi-v7a"
        }
    }

    //debug和release版本的签名配置
    signingConfigs {
        release {
            storeFile file(props['release.keystore'])

            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']
            storePassword = props['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true//去掉不用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            shrinkResources false//去掉不用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        common {
        }
        policeStore {
        }
        policeStoreOut {
        }
        xiangYang {
        }
        xiangYangPoliceStore {
        }
        teJing {
        }
        chuTianYun {
        }
        jiaXing {
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [apk_type: name]
    }

    //版本比较多时，自定义导出的APK名称，不同的渠道编出的APK的文件名应该是不一样的
    //Android插件3.0迁移指南所示：
    //用all()而不是each()
    //如果您只更改文件名（这是您的情况），请使用outputFileName而不是output.outputFile
    applicationVariants.all { variant ->
        variant.outputs.all() { output ->
            def flavor = variant.productFlavors[0]
            def fileName = "uav_${variant.flavorName}_${signTime()}.${variant.versionName}_${variant.versionCode}.apk"
            /*//默认打包方式，打包到app的子目录下
            outputFileName = fileName*/
            //修改打包路径，工程apkpackage文件夹下
            //也可以在打包时手动选择对应目录
            def des = output.packageApplication.outputDirectory.toPath().relativize(rootDir.toPath()).toFile()
            outputFileName = new File("$des/apkpackage", fileName)
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/plexus/components.xml'
        exclude 'licenses/javolution.license.TXT'
        exclude 'licenses/thoughtworks.TXT'
        exclude 'org/apache/maven/project/pom-4.0.0.xml'
        exclude 'licenses/extreme.indiana.edu.license.TXT'
        exclude 'org/cyberneko/html/res/HTMLlat1.properties'
        exclude 'org/cyberneko/html/res/ErrorMessages_ja.properties'
        exclude 'org/cyberneko/html/res/ErrorMessages_ja.txt'
        exclude 'org/cyberneko/html/res/ErrorMessages.properties'
        exclude 'org/codehaus/plexus/plexus-bootstrap.xml'
        exclude 'org/cyberneko/html/res/HTMLspecial.properties'
        exclude 'org/cyberneko/html/res/XMLbuiltin.properties'
        exclude 'org/cyberneko/html/res/HTMLsymbol.properties'

        doNotStrip "*/*/libdjivideo.so"
        doNotStrip "*/*/libSDKRelativeJNI.so"
        doNotStrip "*/*/libFlyForbid.so"
        doNotStrip "*/*/libduml_vision_bokeh.so"
        doNotStrip "*/*/libyuv2.so"
        doNotStrip "*/*/libGroudStation.so"
        doNotStrip "*/*/libFRCorkscrew.so"
        doNotStrip "*/*/libUpgradeVerify.so"
        doNotStrip "*/*/libFR.so"
        exclude 'META-INF/rxjava.properties'
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }


    lintOptions {
        ignoreWarnings true
        checkReleaseBuilds false
    }
}

def signTime() {
    return new Date().format("MM-dd")
}


repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':FsCommPDT')
    implementation('com.dji:dji-sdk:4.9', {
//  Uncomment the following line if your app does not need Anti Distortion for
//  Mavic 2 Pro and Mavic 2 Zoom. It will greatly reducing the size of the APK:
        exclude module: 'library-anti-distortion'
    })
    compileOnly 'com.dji:dji-sdk-provided:4.9'
    implementation 'com.dji:dji-uxsdk:4.9'
}


allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}
